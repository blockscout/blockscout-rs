default:
    just --list --unsorted

db-host := env_var_or_default('DB_HOST', "localhost")
db-port := env_var_or_default('DB_PORT', "5432")
db-user := env_var_or_default('DB_USER', "postgres")
db-password := env_var_or_default('DB_PASSWORD', "admin")
db-name := env_var_or_default('DB_NAME', "scoutcloud")
export DATABASE_URL := "postgres://" + db-user + ":" + db-password + "@" + db-host + ":" + db-port + "/" + db-name

docker-name := env_var_or_default('DOCKER_NAME', "scoutcloud-postgres")
test-db-port := env_var_or_default('TEST_DB_PORT', "9433")

docker-build *args:
    docker build --build-context proto=../proto . {{args}}

start-postgres:
    docker run \
      -p {{db-port}}:5432 \
      --name {{docker-name}} \
      -e POSTGRES_PASSWORD={{db-password}} \
      -e POSTGRES_USER={{db-user}} \
      --rm \
      -d \
      postgres
    sleep 3
    just _create-db

start-tls-postgres:
    mkdir -p tls
    openssl req -new -text -passout pass:abcd -subj /CN=localhost -out tls/server.req -keyout tls/privkey.pem
    openssl rsa -in tls/privkey.pem -passin pass:abcd -out tls/server.key
    openssl req -x509 -in tls/server.req -text -key tls/server.key -out tls/server.crt
    chmod 600 tls/server.key
    -test $(uname -s) = Linux && chown 70 tls/server.key

    docker run \
      -p {{db-port}}:5432 \
      --name {{docker-name}} \
      -e POSTGRES_PASSWORD={{db-password}} \
      -e POSTGRES_USER={{db-user}} \
      -v $(pwd)/tls:/etc/postgresql/certs \
      --rm \
      -d \
      postgres \
      -N 500 \
      -c ssl=on \
      -c ssl_cert_file=/etc/postgresql/certs/server.crt \
      -c ssl_key_file=/etc/postgresql/certs/server.key
    sleep 3
    just _create-db

_create-db:
    # wait for postgres to start, but only if db_name is not empty
    $SHELL -c '[[ -z "{{db-name}}" ]] || docker exec -it {{docker-name}} psql -U postgres -c "create database {{db-name}};"'

stop-postgres:
    docker kill {{docker-name}}

test *args:
    cargo test {{args}} -- --include-ignored

test-with-db *args:
    -just db-port="{{test-db-port}}" db-name="" docker-name="{{docker-name}}-test" start-postgres
    just  db-port="{{test-db-port}}" db-name=""                                    test {{args}}

stop-test-postgres:
    just docker-name="{{docker-name}}-test" stop-postgres

run:
    SCOUTCLOUD__DATABASE__CONNECT__URL={{DATABASE_URL}} cargo run --bin scoutcloud-server

generate-entities *args:
    sea-orm-cli generate entity --lib -o scoutcloud-entity/src {{args}}

new-migration name:
    sea-orm-cli migrate generate -d scoutcloud-migration {{name}}

migrate-up:
    sea-orm-cli migrate -d scoutcloud-migration up

migrate-down:
    sea-orm-cli migrate -d scoutcloud-migration down

insert-test-data:
    docker exec -i {{docker-name}} psql -U {{db-user}} -d {{db-name}} -c "\
    insert into users(id,email) values (1, 'lymarenko.lev@gmail.com');\
    insert into auth_tokens(user_id) values (1); select token_value from auth_tokens;\
    insert into balance_changes(user_id, amount) values (1, 100);"

check-envs:
    cargo run --bin check-envs
