# just is like `make` but better https://github.com/casey/just

# list all recipes
default:
    just --list --unsorted

start-postgres:
    # we run it in --rm mode, so all data will be deleted after stopping
    docker run -p 5432:5432 --name stats-postgres -e POSTGRES_PASSWORD=admin --rm -d postgres:13.8
    # wait for postgres to start
    sleep 3
    docker exec -it stats-postgres psql -U postgres -c 'create database stats;'

stop-postgres:
    docker kill stats-postgres

restart-postgres:
    just stop-postgres || true
    just start-postgres

test database_url *args:
    DATABASE_URL={{database_url}} cargo test {{args}}

# runs with default database_url
test-default *args: (test "postgres://postgres:admin@localhost:5432/" args)

restart-generate-entities: 
    just restart-postgres
    just stats/migrate-up
    just stats/generate-entities
    just stop-postgres
